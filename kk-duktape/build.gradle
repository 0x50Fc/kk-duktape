apply plugin: 'com.android.library'
apply plugin: 'maven'

def getNdkDir(){
    if (System.env.ANDROID_NDK_ROOT != null)
        return System.env.ANDROID_NDK_ROOT
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkdir = properties.getProperty('ndk.dir', null)
    if (ndkdir == null)
        throw new GradleException("NDK location not found. Define location with ndk.dir in the local.properties file or with an ANDROID_NDK_ROOT environment variable.")
    return ndkdir
}

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion MIN_SDK_VERSION as int
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            moduleName "kk-duktape"
            abiFilters "armeabi-v7a"
        }
    }

    sourceSets {
        main() {
            jni.srcDirs = []
        }
    }

    task ndkBuild(type:Exec,description:'Compile JNI source via NDK'){
        def ndkDir = getNdkDir()

        commandLine "$ndkDir/ndk-build",//配置ndk的路径
                'NDK_PROJECT_PATH=build/intermediates/ndk',//ndk默认的生成so的文件
                'NDK_LIBS_OUT=src/main/jniLibs',//配置的我们想要生成的so文件所在的位置
                'NDK_APPLICATION_MK=src/main/jni/Application.mk',//指定项目以这个mk的方式
                'APP_BUILD_SCRIPT=src/main/jni/Android.mk'//指定项目以这个mk的方式
    }
    tasks.withType(JavaCompile){//使用ndkBuild
        compileTask ->compileTask.dependsOn ndkBuild
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
//    compile project(':kk-script')
    compile 'com.sina.weibo.lib.kk:script:' + CURRENTVERSION
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: MAVEN_REPO_RELEASE_URL) {
                authentication(userName: USERNAME, password: PASSWORD)
            }
            pom.project {
                version DEPENDENCIESVERSION
                artifactId 'duktape'
                groupId GROUPID
                packaging TYPE
                description DESCRIPTION
            }
        }
    }
}
